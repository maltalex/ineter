apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.maltalex'
archivesBaseName = 'ineter'
version = '0.1.1'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
	required {
		gradle.taskGraph.hasTask("uploadArchives")
	}
    sign configurations.archives
}

def OSSUsername = hasProperty('sonatypeUsername') ? sonatypeUsername : "dummyOssUsername"
def OSSPassword = hasProperty('sonatypePassword') ? sonatypePassword : "dummyOssPassword"

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment {
				MavenDeployment deployment -> signing.signPom(deployment) 
      		}

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: OSSUsername, password: OSSPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: OSSUsername, password: OSSPassword)
			}

			pom.project {
				name 'ineter'
				packaging 'jar'
				description ' Fast Java library for working with IP addresses, ranges, and subnets '
				url 'https://github.com/maltalex/Ineter'

				scm {
					url 'https://github.com/maltalex/Ineter'
					connection 'scm:git:https://github.com/maltalex/Ineter'
					developerConnection 'scm:git@github.com:maltalex/Ineter'
				}

				licenses {
					license {
						name 'Mozilla Public License 2.0'
						url 'http://mozilla.org/MPL/2.0/'
					  }
				}

				developers {
					developer {
						id 'maltalex'
						name 'Alex Maltinsky'
						email 'code@bit48.net'
					}
				}
			}
		}
	}
}